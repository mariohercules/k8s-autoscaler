apiVersion: v1
kind: ConfigMap
metadata:
  creationTimestamp: null
  name: strategies
data:
  strategies.s: |
    module kubow.strategies;
    import model "KubeZnnSystem:Acme" { KubeZnnSystem as M, KubernetesFam as K };
    import lib "tactics.s";
    import op "org.sa.rainbow.model.stitch.*";

    define boolean textMode = org.sa.rainbow.stitch.Operators.containerImage(M.kubeZnnD, "znn", "cmendes/znn:text");
    define boolean lowMode = org.sa.rainbow.stitch.Operators.containerImage(M.kubeZnnD, "znn", "cmendes/znn:low");
    define boolean highMode = org.sa.rainbow.stitch.Operators.containerImage(M.kubeZnnD, "znn", "cmendes/znn:high");
    define boolean cHiRespTime = M.kubeZnnS.latency > M.MAX_RESPTIME;
    define boolean cLoRespTime = M.kubeZnnS.latency < M.MIN_RESPTIME;
    define boolean canAddPod = M.kubeZnnD.maxReplicas > M.kubeZnnD.desiredReplicas;
    define boolean canRemovePod = M.kubeZnnD.minReplicas < M.kubeZnnD.desiredReplicas;

    strategy ReduceRespTime [ cHiRespTime ] {
      t0: (cHiRespTime && canAddPod) -> addReplicas(1) @[15000 /*ms*/] {
        t0a: (success) -> done;
      }
      t1: (cHiRespTime && !canAddPod) -> lowerFidelity() @[15000 /*ms*/] {
        t1a: (success) -> done;
      }
      t2: (default) -> TNULL;
    }

    strategy ReduceCost [ cLoRespTime && highMode ] {
      t0: (cLoRespTime && canRemovePod && highMode) -> removeReplicas(1) @[15000 /*ms*/] {
        t0a: (success) -> done;
      }
      t1: (default) -> TNULL;
    }

    strategy ImproveFidelity [ cLoRespTime && (textMode || lowMode) ] {
      t0: (cLoRespTime && (textMode || lowMode)) -> raiseFidelity() @[15000 /*ms*/] {
        t0a: (success) -> done;
      }
      t1: (default) -> TNULL;
    }
